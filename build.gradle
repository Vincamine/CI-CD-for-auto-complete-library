plugins {
    id 'java'
    id 'java-library'
    id 'checkstyle'
    id 'pmd'
    id 'jacoco'
    id 'maven-publish'
    id 'signing'
    id 'com.github.spotbugs' version "6.1.3"
}

group = 'io.github.Vincamine'
version = '0.1.0'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
    testImplementation 'org.mockito:mockito-core:4.8.0'
    implementation 'org.slf4j:slf4j-api:2.0.9'
    implementation 'ch.qos.logback:logback-classic:1.4.11'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.70
            }
        }
    }
}

checkstyle {
     toolVersion = "10.12.1"
     configFile = file("config/checkstyle/checkstyle.xml")
}

pmd {
    ruleSets = []
    toolVersion = '6.54.0'
    ruleSetFiles = files("config/pmd/ruleset.xml")
    ignoreFailures = true
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'autocomplete'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'AutoCompleteLibrary'
                description = 'A Java library for providing auto-completion of English words'
                url = 'https://github.com/CS6510-SEA-SP25/hw1-Vincamine'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'Vincamine'
                        name = 'Wyllie'
                        email = 'wylliefang@gmail.com'
                    }
                }
            }
        }
    }
}

jar {
    manifest {
        attributes(
            'Main-Class': 'autowordcomplete.AutoCompleteMain'
        )
    }
}
